import Austral.Memory (
    Address,
    Pointer,
    allocateBuffer,
    resizeArray,
    deallocate,
    nullCheck,
    positiveOffset,
    store,
    load
);

module body Standard.Buffer is
    -- Need this to use Austral.Memory.
    pragma Unsafe_Module;

    """
    The growth factor times the array's capacity tells us the next size of
    the array when resizing. Naively, we double the size of the array when
    hitting the capacity.
    """
    constant Growth_Factor: Index := 2;

    record Buffer[T: Type]: Linear is
        -- The capacity is the true underlying size. It is different from the
        -- size since we allocate more space than we need in order to minimize
        -- frequent reallocation.
        capacity: Index;
        -- The number of elements actually stored in the array.
        size: Index;
        -- The underlying heap array.
        array: Pointer[T];

        -- Invariants:
        --
        --   capacity >= size
    end;

    --
    -- Lifecycle functions.
    --

    generic [T: Free]
    function initialize(size: Index, initialElement: T): Option[Buffer[T]] is
        let addr: Address[T] := allocateBuffer(size);
        case nullCheck(addr) of
            when Some(value as array: Pointer[T]) do
                for i from 0 to size - 1 do
                    let nth: Pointer[T] := positiveOffset(array, i);
                    store(nth, initialElement);
                end for;
                let buf: Buffer[T] := Buffer(
                    capacity => size,
                    size => size,
                    array => array
                );
                return Some(value => buf);
            when None do
                return None();
        end case;
    end;

    generic [T: Free]
    function destroyFree(buffer: Buffer[T]): Unit is
        let { capacity: Index, size: Index, array: Pointer[T] } := buffer;
        deallocate(array);
        return nil;
    end;

    generic [T: Type]
    function destroyEmpty(buffer: Buffer[T]): Unit is
        let { capacity: Index, size: Index, array: Pointer[T] } := buffer;
        if size /= 0 then
            abort("destroyEmpty called with a non-empty buffer.");
        end if;
        deallocate(array);
        return nil;
    end;

    --
    -- Query functions.
    --

    generic [T: Free, R: Region]
    function bufferSize(buffer: &[Buffer[T], R]): Index is
        return !(buffer->size);
    end;

    ---
    --- Retrieval functions.
    ---

    generic [T: Free, R: Region]
    function nthFree(buffer: &[Buffer[T], R], index: Index): T is
        checkInBounds(buffer, index);
        let arr: Pointer[T] := !(buffer->array);
        return load(positiveOffset(arr, index));
    end;

    generic [T: Free, R: Region]
    function nthOpt(buffer: &[Buffer[T], R], index: Index): Option[T] is
        if index >= bufferSize(buffer) then
            return None();
        else
            let arr: Pointer[T] := !(buffer->array);
            let val: T := load(positiveOffset(arr, index));
            return Some(value => val);
        end if;
    end;

    ---
    --- Internal.
    ---

    generic [T: Free, R: Region]
    function checkInBounds(buffer: &[Buffer[T], R], index: Index): T is
        if index >= bufferSize(buffer) then
            abort("Index out of bounds.");
        end if;
    end;
end module body.