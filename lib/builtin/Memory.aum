module body Austral.Memory is
    record Address[T: Type]: Free is end;
    record Pointer[T: Type]: Free is end;

    generic [T: Type]
    function nullPointer(): Address[T] is
        return @embed(Address[T], "NULL");
    end;

    generic [T: Type]
    function nullCheck(address: Address[T]): Option[Pointer[T]] is
        let n: Address[T] := nullPointer();
        if address /= n then
            let ptr: Pointer[T] := @embed(Pointer[T], "$1", address);
            let opt: Option[Pointer[T]] := Some(value => ptr);
            return opt;
        else
            let res: Option[Pointer[T]] := None();
            return res;
        end if;
    end;

    generic [T: Type]
    function Allocate(size: Index): Address[T] is
        return @embed(Address[T], "au_calloc($1, $2)", sizeof(T), size);
    end;

    generic [T: Type]
    function Load(pointer: Pointer[T]): T is
        return @embed(T, "*($1)", pointer);
    end;

    generic [T: Type]
    function Store(pointer: Pointer[T], value: T): Unit is
        @embed(Unit, "AU_STORE($1, $2)", pointer, value);
        return nil;
    end;

    generic [T: Type]
    function Deallocate(pointer: Pointer[T]): Unit is
        @embed(Unit, "au_free($1)", pointer);
        return nil;
    end;

    generic [T: Type, R: Region]
    function Load_Read_Reference(ref: &[Pointer[T], R]): &[T, R] is
        return @embed(&[T, R], "*($1)", ref);
    end;

    generic [T: Type, R: Region]
    function Load_Write_Reference(ref: &![Pointer[T], R]): &![T, R] is
        return @embed(&![T, R], "*($1)", ref);
    end;

    generic [T: Type]
    function Resize_Array(array: Pointer[T], size: Index): Pointer[T] is
        let total: Index := (sizeof(T)) * size;
        return @embed(Pointer[T], "au_realloc($1, $2)", array, total);
    end;

    generic [T: Type, U: Type]
    function memmove(source: Pointer[T], destination: Pointer[U], count: Index): Unit is
        @embed(Pointer[T], "au_memmove($1, $2, $3)", destination, source, count);
        return nil;
    end;

    generic [T: Type, U: Type]
    function memcpy(source: Pointer[T], destination: Pointer[U], count: Index): Unit is
        @embed(Pointer[T], "au_memcpy($1, $2, $3)", destination, source, count);
        return nil;
    end;

    generic [T: Type]
    function Positive_Offset(pointer: Pointer[T], offset: Index): Pointer[T] is
        return @embed(Pointer[T], "$1 + $2", pointer, offset);
    end;

    generic [T: Type]
    function Negative_Offset(pointer: Pointer[T], offset: Index): Pointer[T] is
        return @embed(Pointer[T], "$1 - $2", pointer, offset);
    end;
end module body.